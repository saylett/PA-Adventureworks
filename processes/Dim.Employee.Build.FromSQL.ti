#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

## This Process Builds the Employee Dimension ##

cDimName = 'Employee';
cTopNode = 'All Employees By Dept';

#DimensionDeleteallElements(cDimName);

## Create Dimension if it does not exist ##
IF ( DimensionExists(cDimName) = 0 ) ;
  DimensionCreate(cDimName);
ENDIF;

Dimensionsortorder(cDimName,'ByInPut','Ascending','ByHierarchy','Ascending');

## Insert constant elements ##
DimensionElementInsert(cDimName,'',cTopNode,'C');


## Unwind the Dimension ##
# Iterate through dimensions and for each consolidated element, disconnect its components
nElementCount = DIMSIZ ( cDimName );
iCount = 1;

WHILE ( iCount <= nElementCount );
  sElement = DIMNM ( cDimName, iCount );
  IF ( DTYPE ( cDimName, sElement ) @= 'C' );
    nChildren = ELCOMPN ( cDimName, sElement );
    iChild = nChildren;
    WHILE ( iChild > 0 );
      sChild = ELCOMP ( cDimName, sElement, iChild );
      DimensionElementComponentDelete ( cDimName, sElement, sChild );
      iChild = iChild - 1;
    END;
  ENDIF;
  iCount = iCount + 1;
END;


## Insert Attributes ##
cAttr1 = 'Full Name';


AttrInsert(cdimName,'',cAttr1,'A');
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****

## Clean Metadata ##
sEmployeeID = Trim(vEmployeenationalidalternatekey);

## Insert n Level Elements and rollup into Associated Parent ##
DimensionElementInsert(cDimName,'',sEmployeeID, 'N');
DimensionElementInsert(cDimName,'',vDepartmentname,'C');

DimensionElementcomponentAdd(cDimName,vDepartmentName,sEmployeeID,1);
DimensionElementcomponentAdd(cDimName,cTopNode,vDepartmentName,1);
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****

## Clean Metadata ##
sEmployeeID = Trim(vEmployeenationalidalternatekey);


## Insert Attributes ##
cAttr = 'Full Name';
sValue = vFirstName | ' ' | vLastName | ' ' | vMiddleName;
AttrPutS(sValue,cDimName,sEmployeeID,cAttr);
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion