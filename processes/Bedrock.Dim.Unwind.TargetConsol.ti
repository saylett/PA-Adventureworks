#region Prolog

######################################
##~~  Copyright Cubewise P/L 2010 ~~##
######################################

# This process will remove all children from a specific target consolidation in the target dimension
# If recursive will also unwind all consolidations that are children of the target regardless of depth

# Note:
# - If consolidations are also used in unrelated hierarchies and recursive is selected this will result in
#   orphan consolidations in the other hierarchies


### Constants ###

cProcess = 'Bedrock.Dim.AllConsols.Delete';
cTimeStamp = TimSt( Now, '\Y\m\d\h\i\s' );
cDebugFile = GetProcessErrorFileDirectory | cProcess | '.' | cTimeStamp | '.';
cHierAttr = 'Bedrock.Descendant';
cAttrVal = 'Descendant';


### Initialise Debug ###

If( pDebug >= 1 );

  # Set debug file name
  sDebugFile = cDebugFile | 'Prolog.debug';

  # Log start time
  AsciiOutput( sDebugFile, 'Process Started: ' | TimSt( Now, '\d-\m-\Y \h:\i:\s' ) );

  # Log parameters
  AsciiOutput( sDebugFile, 'Parameters: pDimension : ' | pDimension );
  AsciiOutput( sDebugFile, '            pConsol    : ' | pConsol );
  AsciiOutput( sDebugFile, '            pRecursive : ' | NumberToString( pRecursive ) );

EndIf;


### Validate Parameters ###

nErrors = 0;

# Validate dimension
If( Trim( pDimension ) @= '' );
  nErrors = 1;
  sMessage = 'No dimension specified';
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, sMessage );
  EndIf;
  DataSourceType = 'NULL';
  ItemReject( sMessage );
EndIf;
If( DimensionExists( pDimension ) = 0 );
  nErrors = 1;
  sMessage = 'Invalid dimension: ' | pDimension;
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, sMessage );
  EndIf;
  DataSourceType = 'NULL';
  ItemReject( sMessage );
EndIf;

# Validate consol
If( Trim( pConsol ) @= '' );
  nErrors = 1;
  sMessage = 'No consol specified';
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, sMessage );
  EndIf;
  DataSourceType = 'NULL';
  ItemReject( sMessage );
EndIf;
If( DimIx( pDimension, pConsol ) = 0 % DType( pDimension, pConsol ) @<> 'C' % ElCompN( pDimension, pConsol ) = 0 );
  nErrors = 1;
  sMessage = 'Invalid consolidation: ' | pConsol | '. Not a C element or no children.';
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, sMessage );
  EndIf;
  DataSourceType = 'NULL';
  ItemReject( sMessage );
EndIf;


### Set Descendent attribute value

AttrDelete( pDimension, cHierAttr );
AttrInsert( pDimension, '', cHierAttr, 'S' );

nElementIndex = 1;
nElementCount = DimSiz( pDimension );
While( nElementIndex <= nElementCount );
  sElement = DimNm( pDimension, nElementIndex );
  If( ElIsAnc( pDimension, pConsol, sElement ) = 1 );
    AttrPutS( cAttrVal, pDimension, sElement, cHierAttr );
  EndIf;
  nElementIndex = nElementIndex + 1;
End;


### Remove direct children from the target consol ###

nChildren = ElCompN( pDimension, pConsol );

While( nChildren > 0 );
  # Unwind direct children of target hierarchy only
  sChild = ElComp( pDimension, pConsol, nChildren );
  If( pDebug <= 1 );
    DimensionElementComponentDelete( pDimension, pConsol, sChild );
  EndIf;
  nChildren = nChildren - 1;
End;


### Assign data source ###

If( pRecursive = 1 & pDebug <= 1 );
  # Assign Data Source
  DataSourceType = 'SUBSET';
  DatasourceNameForServer = pDimension;
  DatasourceNameForClient = pDimension;
  DatasourceDimensionSubset = 'ALL';
Else;
  # No data source, straight to Epilog
  DataSourceType = 'NULL';
EndIf;


### End Prolog ###
#endregion
#region Metadata

######################################
##~~  Copyright Cubewise P/L 2010 ~~##
######################################


### Check for errors in prolog ###

If( nErrors <> 0 );
  ProcessBreak;
EndIf;


### Break all parent/child links below target consol ###

If( ElLev( pDimension, vElement ) = 0 );
  ItemSkip;
EndIf;

If( AttrS( pDimension, vElement, cHierAttr ) @= cAttrVal & DType( pDimension, vElement ) @= 'C' & ElCompN( pDimension, vElement ) > 0 );
  nChildren = ElCompN( pDimension, vElement );
  While( nChildren > 0 );
    sChild = ElComp( pDimension, vElement, nChildren );
    If( pDebug <= 1 );
      DimensionElementComponentDelete( pDimension, vElement, sChild );
    EndIf;
    nChildren = nChildren - 1;
  End;
EndIf;


### End Metadata ###
#endregion
#region Epilog

######################################
##~~  Copyright Cubewise P/L 2010 ~~##
######################################


### Remove Descendent attribute

If( nErrors = 0 & pDebug <= 1 );
  AttrDelete( pDimension, cHierAttr );
EndIf;


### Initialise Debug ###

If( pDebug >= 1 );

  # Set debug file name
  sDebugFile = cDebugFile | 'Epilog.debug';

  # Log errors
  If( nErrors <> 0 );
    AsciiOutput( sDebugFile, 'Errors Occurred' );
  EndIf;

  # Log finish time
  AsciiOutput( sDebugFile, 'Process Finished: ' | TimSt( Now, '\d-\m-\Y \h:\i:\s' ) );

EndIf;


### If errors occurred terminate process with a major error status ###

If( nErrors <> 0 );
  ProcessQuit;
EndIf;


### End Epilog ###
#endregion