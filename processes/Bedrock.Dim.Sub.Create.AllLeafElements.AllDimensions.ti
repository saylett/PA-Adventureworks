#region Prolog

######################################
##~~  Copyright Cubewise P/L 2010 ~~##
######################################

# This process will create all N-Level Subsets


### Constants ###

cProcess = 'Bedrock.Dim.Sub.Create.AllLeafElements.AllDimensions';
cTimeStamp = TimSt( Now, '\Y\m\d\h\i\s' );
cDebugFile = GetProcessErrorFileDirectory | cProcess | '.' | cTimeStamp | '.';
cDimension = '}Dimensions';
cSubset = pSubset;

### Initialise Debug ###

If( pDebug >= 1 );

  # Set debug file name
  sDebugFile = cDebugFile | 'Prolog.debug';

  # Log start time
  AsciiOutput( sDebugFile, 'Process Started: ' | TimSt( Now, '\d-\m-\Y \h:\i:\s' ) );

  # Log parameters
  AsciiOutput( sDebugFile, '            pSubset         : ' | pSubset );
EndIf;


### Validate Parameters ###
# Validate subset
If( Trim( pSubset ) @= '' );
  nErrors = 1;
  sMessage = 'No subset specified';
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, sMessage );
  EndIf;
  ItemReject( sMessage );
EndIf;

### Create Subset ###

nDimensionCount = DimSiz( cDimension );
nDimensionIndex = 1;
While( nDimensionIndex <= nDimensionCount );
  sDimension = DimNm( cDimension, nDimensionIndex );
  sMDX = '{ TM1SORT( { TM1FILTERBYLEVEL( { TM1SUBSETALL( [' | sDimension | '] ) }, 0 ) }, ASC ) }';
  If( SubSt( sDimension, 1, 1 ) @= '}' & SubSt( sDimension, 1, 9 ) @<> '}Bedrock' );
    # Do nothing
  ElseIf( SubsetExists( sDimension, cSubset ) = 1 );
    # Do nothing
  Else;
    If( pDebug <= 1 );
      SubsetCreatebyMDX( cSubset, sMDX );
    EndIf;
    If( pDebug >= 1 );
      AsciiOutput( sDebugFile, 'Dimension: ' | sDimension | ', subset: ' | cSubset | ', MDX: ' | sMDX );
    EndIf;
  EndIf;
  nDimensionIndex = nDimensionIndex + 1;
End;


### End Prolog ###
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

######################################
##~~  Copyright Cubewise P/L 2010 ~~##
######################################


### Initialise Debug ###

If( pDebug >= 1 );

  # Set debug file name
  sDebugFile = cDebugFile | 'Epilog.debug';

  # Log finish time
  AsciiOutput( sDebugFile, 'Process Finished: ' | TimSt( Now, '\d-\m-\Y \h:\i:\s' ) );

EndIf;


### End Epilog ###
#endregion