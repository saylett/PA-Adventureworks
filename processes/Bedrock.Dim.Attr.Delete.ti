#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

######################################
##~~  Copyright Cubewise P/L 2010 ~~##
######################################

### Constants ###

cProcess = 'Bedrock.Dim.Attr.Delete';
cTimeStamp = TimSt( Now, '\Y\m\d\h\i\s' );
cDebugFile = GetProcessErrorFileDirectory | cProcess | '.' | cTimeStamp | '.';


### Initialise Debug ###

If( pDebug >= 1 );

  # Set debug file name
  sDebugFile = cDebugFile | 'Prolog.debug';

  # List parameters
  AsciiOutput( sDebugFile, 'Parameters: pDimension : ' | pDimension );
  AsciiOutput( sDebugFile, '            pAttribute : ' | pAttribute );

EndIf;


### Validate Parameters ###

# Validate dimension
If( Trim( pDimension ) @= '' );
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, 'No dimension specified' );
  EndIf;
  ProcessQuit;
EndIf;
If( DimensionExists( pDimension ) = 0 );
 If( pDebug >= 1 );
    AsciiOutput( sDebugFile, 'Dimension: ' | pDimension | ' does not exist' );
  EndIf;
  ProcessQuit;
EndIf;

# Validate attribute
If( Trim( pAttribute ) @= '' );
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, 'No attribute specified' );
  EndIf;
  ProcessQuit;
EndIf;
If( DimIx( '}ElementAttributes_' | pDimension, pAttribute ) = 0 );
  If( pDebug >= 1 );
    AsciiOutput( sDebugFile, 'Attribute ' | pAttribute | ' does not exist in dimenson: ' | pDimension );
  EndIf;
  ProcessQuit;
EndIf;


### Delete Attribute ###

If( pDebug <= 1 );
  AttrDelete( pDimension, pAttribute );
EndIf;


### End Prolog ###
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion